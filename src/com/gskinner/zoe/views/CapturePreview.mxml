<?xml version="1.0" encoding="utf-8"?>
<!--
ZoÃ« by gskinner.com.
Visit www.gskinner.com/blog for documentation, updates and more free code.

Copyright (c) 2010 Grant Skinner

Permission is hereby granted, free of charge, to any person
obtaining a copy of this software and associated documentation
files (the "Software"), to deal in the Software without
restriction, including without limitation the rights to use,
copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the
Software is furnished to do so, subject to the following
conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
OTHER DEALINGS IN THE SOFTWARE.
-->

<!---

	The view used to display the loaded swf, and its capture bounds.

-->
<s:SkinnableContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" creationComplete="init()"
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 resize="handleResize()" width="100%" height="100%"
		 >
	
	<fx:Script>
		<![CDATA[
			import com.gskinner.zoe.controls.ToolTip;
			import com.gskinner.zoe.events.CaptureEvent;
			import com.gskinner.zoe.model.FileModel;
			import com.gskinner.zoe.utils.DottedLine;
			
			import mx.controls.Alert;
			import mx.core.FlexBitmap;
			import mx.core.UIComponent;
			
			/**
			 * @private
			 * 
			 */
			[Embed(source="../../../../assets/4x4Grid.png")]
			protected var alphaGrid:Class;
			
			/**
			 * @private
			 * 
			 */
			protected const MOVE:String = 'move';
			
			/**
			 * @private
			 * 
			 */
			protected const RESIZE:String = 'resize';
			
			/**
			 * @private
			 * 
			 */
			protected const MIN_WIDTH:uint = 25;
			
			/**
			 * @private
			 * 
			 */
			protected const MIN_HEIGHT:uint = 25;
			
			/**
			 * @private
			 * 
			 */
			protected var dragOffset:Point;
			
			/**
			 * @private
			 * 
			 */
			protected var resizeStartPoint:Point;
			
			/**
			 * @private
			 * 
			 */
			protected var dragMode:String;
			
			/**
			 * @private
			 * 
			 */
			protected var alphaGridBitmap:BitmapData;
			
			/**
			 * @private
			 * 
			 */
			protected var _registrationPoint:Point;
			
			/**
			 * @private
			 * 
			 */
			protected var _source:String;
			
			/**
			 * @private
			 * 
			 */
			protected var _captureBounds:Rectangle;
			
			/**
			 * @private
			 * 
			 */
			protected var _backgroundColor:int = -1;
			
			/**
			 * @private
			 * 
			 */
			protected var _showGrid:Boolean = true;
			
			/**
			 * @private
			 * 
			 */
			public var fileModel:FileModel;
			
			/**
			 * @private
			 * 
			 */
			public var maskContainer:Sprite;
			
			/**
			 * @private
			 * 
			 */
			public var dl:DottedLine;
			
			/**
			 * @private
			 * 
			 */
			public var padding:Number = 0;
			
			/**
			 * @private
			 * 
			 */
			public var regPtClipContainer:UIComponent;
			
			/**
			 * @private
			 * 
			 */
			public var registrationOver:Boolean = false;
			
			/**
			 * @private
			 * 
			 */
			public var dlContainer:UIComponent;
			
			/**
			 * @private
			 * 
			 */
			public var s:Shape;
			
			/**
			 * @private
			 * 
			 */
			public var tt:ToolTip;
			
			/**
			 * @private
			 * 
			 */
			public var isDown:Boolean = false;
			
			/**
			 * @private
			 * 
			 */
			public var showBoundBox:Boolean = true;
			
			protected function init():void {
				alphaGridBitmap = (new alphaGrid() as FlexBitmap).bitmapData;
				
				dragOffset = new Point();
				resizeStartPoint = new Point();
				
				maskContainer = new Sprite();
				
				var cont:UIComponent = new UIComponent();
				cont.addChild(maskContainer);
				
				var g:Graphics = maskContainer.graphics;
				g.clear();
				
				g.beginFill(0xFF0099, 1);
				g.drawRect(0, 0, width, height);
				g.endFill();
				this.addElement(cont);
				
				this.mask = cont;
				
				dl = new DottedLine();
				
				dlContainer = new UIComponent();
				dlContainer.mouseChildren = false;
				dlContainer.mouseEnabled = false;
				dlContainer.addChild(dl);
				
				addElement(dlContainer);
				
				regPtClipContainer = new UIComponent();
				addElement(regPtClipContainer);
				
				s = new Shape();
				
				regPtClipContainer.useHandCursor = true;
				regPtClipContainer.buttonMode = true;
				
				regPtClipContainer.mouseChildren = false;
				regPtClipContainer.doubleClickEnabled = true;
				
				regPtClipContainer.addEventListener(MouseEvent.DOUBLE_CLICK, handleDoubleClick, false, 0, true);
				regPtClipContainer.addEventListener(MouseEvent.MOUSE_DOWN, handleRegPointDown, false, 0, true);
				regPtClipContainer.addEventListener(MouseEvent.MOUSE_UP, handleRegPointUp, false, 0, true);
				regPtClipContainer.addEventListener(MouseEvent.MOUSE_MOVE, handleRegPointMove, false, 0, true);
				
				tt = new ToolTip();
				tt.hide();
				addElement(tt);
				
				NativeApplication.nativeApplication.addEventListener(KeyboardEvent.KEY_DOWN, onKeyDown, true);
			}
			
			/**
			 * Reloads the swf from disk.
			 * 
			 */
			public function refresh():void {
				swfLoader.unloadAndStop();
				var oldSource:String = _source;
				_source = null;
				source = oldSource;
				swfLoader.loaderInfo.uncaughtErrorEvents.addEventListener(UncaughtErrorEvent.UNCAUGHT_ERROR, handleLoadedSwfError);
			}
			
			/**
			 * Sets the current items background color.
			 * 
			 */
			public function setBackgroundColor(showGrid:Boolean, color:int):void {
				_backgroundColor = color;
				_showGrid = showGrid;
				updateView();
			}
			
			/**
			 * Sets the source swf file.
			 * 
			 */
			public function set source(url:String):void {
				if (_source == url) { return; }
				
				_source = url;
				swfLoader.loaderContext = new LoaderContext(false);
				swfLoader.loaderContext.allowCodeImport = true;
				
				if (url) {
					var file:File = new File(url);
					
					if (file.exists == false) {
						var alert:Alert = Alert.show("Source file doesn't exist.", "Error");
						alert.validateNow();
						return;
					}
					
					var fs:FileStream = new FileStream();
					fs.open(file, FileMode.READ);
					
					var ba:ByteArray = new ByteArray();
					fs.readBytes(ba, 0, file.size);
					fs.close();
					
					swfLoader.load(ba);
				} else {
					_captureBounds = null;
					updateView();
					swfLoader.unloadAndStop();
				}
			}
			
			/**
			 * Gets the swfs current registration point.
			 * The registrationPoint is an optional movieclip placed in the loaded swf's root timeline
			 * it is used to define the registrationPoint for use in Easel.
			 * 
			 */
			public function get registrationPoint():Point {
				return _registrationPoint;
			}
			
			/**
			 * Updates the current animations frame.
			 * 
			 */
			public function gotoAndStop(frameNum:int):void {
				content.gotoAndStop(frameNum);
			}
			
			/**
			 * Plays the loaded swf's animation.
			 * 
			 */
			public function play():void {
				if (content) {
					content.play();
				}
			}
			
			/**
			 * Stops the loaded swf form playing.
			 * 
			 */
			public function stop():void {
				content.stop();
			}
			
			/**
			 * Gets the current frame, or -1 if no content exists.
			 * 
			 */
			public function get currentFrame():int {
				return content == null?-1:content.currentFrame;
			}
			
			/**
			 * Gets the total frames from the loaded content.
			 * 
			 */
			public function get totalFrames():int {
				return content == null?0:content.totalFrames;
			}
			
			/**
			 * Updates the current capture baounds, and updates the view.
			 * 
			 */
			public function set captureBounds(bounds:Rectangle):void {
				_captureBounds = bounds.clone();
				updateView();
			}
			
			/**
			 * Returns the current capture bounds.
			 * 
			 */
			public function get captureBounds():Rectangle {
				return _captureBounds;
			}
			
			/**
			 * @private
			 * 
			 */
			public function get content():MovieClip {
				//Dispatch Error Event catch in Main
				return swfLoader.content as MovieClip;
			}
			
			/**
			 * @private
			 * 
			 */
			public function updatePadding():void {
				this.setElementIndex(dlContainer, this.numElements - 1);
				dlContainer.alpha = (padding < 1) ? 0:1; 
				
				if (_captureBounds) {
					dl.update(_captureBounds, padding);
					dl.x = _captureBounds.x;
					dl.y = _captureBounds.y;
				}
			}
			
			/**
			 * @private
			 * 
			 */
			public function displayPadding(value:Boolean):void {
				dlContainer.visible = !value;
			}
			
			/**
			 * @private
			 * 
			 */
			public function findMovieRegPoint():void {
				var registrationPointClip:DisplayObject = content.getChildByName('registrationPoint');
				if (registrationPointClip) {
					var pt:Point = new Point(registrationPointClip.x, registrationPointClip.y);
					regPtClipContainer.x = pt.x;
					regPtClipContainer.y = pt.y;
					this.updateRegPoint();
				} else {
					topAlignRegPoint();
				}
			}
			
			/**
			 * @private
			 * 
			 */
			public function topAlignRegPoint():void {
				var rect:Rectangle = (dlContainer.visible) ? this.dlContainer.getBounds(this) : new Rectangle(_captureBounds.x-1, _captureBounds.y-1, _captureBounds.width, _captureBounds.height);
				var pt:Point = new Point(rect.x, rect.y);
				regPtClipContainer.x = pt.x;
				regPtClipContainer.y = pt.y;
				this.updateRegPoint();
			}
			
			
			/**
			 * @private
			 * 
			 */
			protected function handleLoadedSwfError(event:UncaughtErrorEvent):void {
				event.preventDefault();
			}
			
			/**
			 * @private
			 * 
			 */
			protected function handleResize():void {
				updateView();
				dispatchEvent(new Event(Event.CHANGE));
			}
			
			/**
			 * @private
			 * 
			 */
			protected function updateView():void {
				if (initialized ==  false) { return; }
				var g:Graphics = background.graphics;
				g.clear();
				
				if (_showGrid) {
					g.beginBitmapFill(alphaGridBitmap);
				} else {
					g.beginFill(_backgroundColor, 1);
				}
				
				g.drawRect(0,0, width,height);
				
				if (_captureBounds != null) {
					g.beginFill(0xffffff, .2);
					
					g.lineStyle(1, 0x0, (showBoundBox) ? 1 : 0.1, true);
					var displayBounds:Rectangle = _captureBounds.clone();
					displayBounds.inflate(1, 1); //Inflate by 1 so animation is within the preview.
					g.drawRect(displayBounds.x, displayBounds.y, displayBounds.width, displayBounds.height);
				}
				updateMask();
				updatePadding();
			}
			
			/**
			 * @private
			 * 
			 */
			protected function updateMask():void {
				var g:Graphics = maskContainer.graphics;
				g.clear();
				g.beginFill(0xFF0099, 1);
				g.drawRect(0,0, width, height);
				g.endFill();
			}
			
			/**
			 * Enable key controls for fine-tuned moving of the capture bounds.
			 * 
			 */
			protected function onKeyDown(event:KeyboardEvent):void {
				dragMode = null;
				if (_captureBounds == null) { return; }
				
				if (event.shiftKey) {
					switch (event.keyCode) {
						case Keyboard.LEFT:
							_captureBounds.width--; break;
						case Keyboard.RIGHT:
							_captureBounds.width++; break;
						case Keyboard.UP:
							_captureBounds.height--; break;
						case Keyboard.DOWN:
							_captureBounds.height++; break;
					}
				} else if (event.commandKey) {
					switch (event.keyCode) {
						case Keyboard.LEFT:
							_captureBounds.width--;
							_captureBounds.height--;
							break;
						case Keyboard.RIGHT:
							_captureBounds.width++;
							_captureBounds.height++;
							break;
					}
				} else {
					switch (event.keyCode) {
						case Keyboard.LEFT:
							_captureBounds.x--; break;
						case Keyboard.RIGHT:
							_captureBounds.x++; break;
						case Keyboard.UP:
							_captureBounds.y--; break;
						case Keyboard.DOWN:
							_captureBounds.y++; break;
					}
				}
				
				updateView();
				dispatchEvent(new Event(Event.CHANGE));
			}
			
			/**
			 * @private
			 * 
			 */
			protected function updatePosition():void {
				var newX:Number;
				var newY:Number;
				
				var newWidth:Number;
				var newHeight:Number;
				
				switch (dragMode) {
					case MOVE:
						newX = mouseX - dragOffset.x|0;
						newY = Math.max(mouseY - dragOffset.y, 0)|0;
						
						var requestedX:Number = !isNaN(newX)?newX:_captureBounds.x;
						var requestedY:Number = (!isNaN(newY)?newY:_captureBounds.y);
						
						_captureBounds.x = Math.max(0, Math.min(requestedX, width-_captureBounds.width));
						_captureBounds.y = Math.max(0, Math.min(requestedY, height-_captureBounds.height));
						break;
					case RESIZE:
						newWidth = Math.max(mouseX - resizeStartPoint.x, MIN_WIDTH);
						newHeight = Math.max(mouseY - resizeStartPoint.y, MIN_HEIGHT);
						
						_captureBounds.width = !isNaN(newWidth)?newWidth:_captureBounds.width;
						_captureBounds.height = !isNaN(newHeight)?newHeight:_captureBounds.height;
						break;
				}
				
				updateView();
				dispatchEvent(new Event(Event.CHANGE));
			}
			
			/**
			 * @private
			 * 
			 */
			protected function handleMouseDown():void {
				if (cursorInResizeBounds()) {
					dragMode = RESIZE;
					resizeStartPoint.x = _captureBounds.x + (background.mouseX-(_captureBounds.x+_captureBounds.width));
					resizeStartPoint.y = _captureBounds.y + (background.mouseY-(_captureBounds.y+_captureBounds.height));
				} else if (cursorInCaptureBounds()) {
					dragMode = MOVE;
					dragOffset.x = background.mouseX - _captureBounds.x;
					dragOffset.y = background.mouseY - _captureBounds.y;
				} else {
					dragMode = null;
				}
				
				if (dragMode == null) {
					removeEventListener(MouseEvent.MOUSE_MOVE, handleMoveTransform);
				} else {
					addEventListener(MouseEvent.MOUSE_MOVE, handleMoveTransform);
				}
			}
			
			/**
			 * @private
			 * 
			 */
			protected function handleMosueUp():void {
				removeEventListener(MouseEvent.MOUSE_MOVE, handleMoveTransform);
			}
			
			/**
			 * @private
			 * 
			 */
			protected function handleMoveTransform(event:MouseEvent):void {
				updatePosition();
			}
			
			/**
			 * @private
			 * 
			 */
			protected function handleMouseMove():void {
				if (cursorInResizeBounds()) {
					Mouse.cursor = MouseCursor.BUTTON;
				} else if (cursorInCaptureBounds()) {
					
						Mouse.cursor = MouseCursor.HAND;
					
				} else {
					Mouse.cursor = MouseCursor.AUTO;
				}
			}
			
			/**
			 * @private
			 * 
			 */
			protected function cursorInCaptureBounds():Boolean {
				if (_captureBounds == null) { return false; }
				var x:Number = _captureBounds.x;
				var y:Number = _captureBounds.y;
				var w:Number = _captureBounds.width;
				var h:Number = _captureBounds.height;
				
				if (mouseX < x + w && mouseX > x && mouseY < y + h && mouseY > y) {
					return true;
				} else {
					return false;
				}
			}	
			
			/**
			 * @private
			 * 
			 */
			protected function cursorInResizeBounds():Boolean {
				if (_captureBounds == null) { return false; }
				
				var x:Number = _captureBounds.x;
				var y:Number = _captureBounds.y;
				var w:Number = _captureBounds.width;
				var h:Number = _captureBounds.height;
				
				if (mouseX < x + w && mouseX > w+x - 10
					&& mouseY < y + h && mouseY > h+y - 10
				) {
					return true;
				} else {
					return false;
				}
			}
			
			/**
			 * @private
			 * 
			 */
			protected function handleSwfLoad():void {
				if (swfLoader.content is AVM1Movie) {
					Alert.show('Your SWF is not supported, please use a Flash 9 or greater swf.', 'Unsupported SWF');
					fileModel.deleteSelected();
					return;
				}
				
				if (!(swfLoader.content is MovieClip)) {
					Alert.show('Your SWF is not supported, please ensure target is a MovieClip.', 'Unsupported SWF');
					fileModel.deleteSelected();
					return;
				}
				
				if (content == null) { return; }
				
				var registrationPointClip:DisplayObject = content.getChildByName('registrationPoint');
				if (registrationPointClip) {
					registrationPointClip.visible = false;
					
					_registrationPoint = new Point(registrationPointClip.x, registrationPointClip.y);
					
				} else {
					var p:Point = getLocalPoint();
					_registrationPoint = p;
				}
				dispatchEvent(new CaptureEvent(CaptureEvent.SWF_INIT));
			}
			
			/**
			 * @private
			 * 
			 */
			protected function handleRegPointDown(event:MouseEvent):void {
				tt.show(); 
				var clip:UIComponent = (event.currentTarget as UIComponent);
				setElementIndex(clip, this.numElements - 1);
				setElementIndex(tt, this.numElements - 1);
				clip.startDrag();
				isDown = true;
			}
			
			/**
			 * @private
			 * 
			 */
			
			protected function handleDoubleClick(event:MouseEvent):void {
				topAlignRegPoint();
			}
			
			/**
			 * @private
			 * 
			 */
			protected function handleRegPointMove(event:MouseEvent):void {
				Mouse.cursor = MouseCursor.BUTTON;
				tt.labelField = 'Reg Point ' + getLocalPoint();
				tt.x = mouseX + 5;
				tt.y = mouseY + 25;
				
				checkBoundary();
				
			}
			
			/**
			 * @private
			 * 
			 */
			protected function checkBoundary():void {
				var rect:Rectangle = this.background.getRect(background);
				if (mouseX + tt.actualWidth > rect.right) {
					tt.x = rect.right - tt.actualWidth;
				} else if (mouseX < rect.left) {
					tt.x = rect.left + tt.actualWidth;
				} 
				if (mouseY + tt.actualHeight > (rect.bottom - (tt.actualHeight+5))) {
					tt.y = rect.bottom - tt.actualHeight;
				} else if (mouseY - tt.actualHeight < rect.top) {
					tt.y = rect.top + tt.actualHeight;
				}
			}
			
			/**
			 * @private
			 * 
			 */
			protected function handleRegPointUp(event:MouseEvent):void {
				tt.hide();
				var clip:UIComponent = (event.currentTarget as UIComponent);
				clip.stopDrag();
				updateRegPoint();
				isDown = false;
			}
			
			/**
			 * @private
			 * 
			 */
			public function updateRegPoint():void {
				var p:Point = getLocalPoint();
				var displayPt:Point = new Point();
				if (registrationPoint) {
					_registrationPoint.x = p.x;
					_registrationPoint.y = p.y;
					
					
				}else {
					_registrationPoint = new Point(p.x, p.y);
				}
				
				displayPt.x = regPtClipContainer.x;
				displayPt.y = regPtClipContainer.y;
				
				dispatchEvent(new CaptureEvent(CaptureEvent.REG_PT_CHANGE, false, false, null, {displayPt:displayPt, relativePt:registrationPoint}));
			}
			
			/**
			 * @private
			 * 
			 */
			protected function getLocalPoint():Point {
				var rect:Rectangle = (dlContainer.visible) ? this.dlContainer.getBounds(this) : new Rectangle(_captureBounds.x-1, _captureBounds.y-1, _captureBounds.width, _captureBounds.height);
				
				s.graphics.clear();
				s.graphics.beginFill(0x0, 1);
				s.graphics.drawRect(0, 0, rect.width, rect.height);
				s.graphics.endFill();
				s.x = rect.x;
				s.y = rect.y;
				
				return s.globalToLocal(new Point(this.regPtClipContainer.x, this.regPtClipContainer.y));
			}
			
			/**
			 * @private
			 * 
			 */
			protected function handleError():void {
				//Surpress
			}
			
			/**
			 * @private
			 * 
			 */
			protected function createRegisterPoint():void {
				if (regPtClipContainer.numChildren == 0) {
					regPtClipContainer.addChild(getSprite());
				}
				
				var pt:Point;
				
				if (this.fileModel.selectedItem.displayPt) {
					pt = this.fileModel.selectedItem.displayPt
				} else if (_registrationPoint != null) {
					pt = _registrationPoint;
				} else {
					pt = new Point(0, 0);
				}
				
				regPtClipContainer.x = pt.x;
				regPtClipContainer.y = pt.y; 
				
				updateRegPoint();
			}
			
			/**
			 * @private
			 * 
			 */
			public function setRegPt(pt:Point):void {
				createRegisterPoint();
			}
			
			/**
			 * @private
			 * 
			 */
			protected function getSprite():Sprite {
				var s:Sprite = new Sprite();
				var g:Graphics = s.graphics;
				var radius:Number = 5;
				
				g.lineStyle(1, 0x000000, 0, true);
				g.beginFill(0x000000, 0);
				g.drawCircle(0, 0, radius*2);
				g.beginFill(0xFFFFFF, 0);
				g.drawCircle(0, 0, radius-1);
				g.endFill();
				
				g.lineStyle(1, 0x000000, 1, true);
				g.moveTo(-radius,0);
				g.lineTo(radius, 0);
				g.moveTo(0, radius);
				g.lineTo(0, -radius);

				return s;
			}
			
		]]>
	</fx:Script>
	
	<mx:Canvas id="background" width="100%" height="100%"  mouseMove="handleMouseMove()" mouseDown="handleMouseDown()" mouseUp="handleMosueUp()" />
	<mx:SWFLoader id="swfLoader" securityError="handleError()" ioError="handleError()" mouseChildren="false" mouseEnabled="false" init="handleSwfLoad()" scaleContent="false" />
	
</s:SkinnableContainer>
